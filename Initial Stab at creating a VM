// Set up parameters

param VMName string

param AdminUser string
@secure()
param AdminPW string

param VMLocation string = resourceGroup().location
param VMIPAddress string

@allowed([
  'AZ01'
  'AZ02'
])
param VMvNet string

@allowed([
  'DirectoryServices'
  'FileServices'
  'Management'
  'DatabaseServices'
])
param VMSubnet string

@allowed([
  '2016-Datacenter'
  '2019-Datacenter'
])
param OSVersion string

@allowed([
  '128'
  '256'
  '512'
  '1024'
])
param DataDiskSize string

param CostCentreTag string
param FunctionTag string

// Get the ID of the subnet in the required vNet

resource SubnetID 'Microsoft.Network/virtualNetworks/subnets@2021-05-01' existing = {
  name: '${VMvNet}/${VMSubnet}'
  scope: resourceGroup('TM-RG-Networking')
}

// Create the NIC

resource NewNic 'Microsoft.Network/networkInterfaces@2021-05-01' = {
  name: '${VMName}_NIC01'
  location: VMLocation
  properties: {
    ipConfigurations:[
      {
        name: 'ipconfig1'
        properties:{
          privateIPAllocationMethod: 'Static'
          privateIPAddress: VMIPAddress
          subnet:{
            id: SubnetID.id
          }
        }
      }
    ]
  }
}

// Create the VM

resource NewVM 'Microsoft.Compute/virtualMachines@2021-11-01' = {
  name: VMName
  location: VMLocation
  tags: {
    CostCentre: CostCentreTag
    function: FunctionTag
  }
  properties: {
    hardwareProfile: {
      vmSize: 'Standard_B2s'
    }
    osProfile:{
      computerName: VMName
      adminUsername: AdminUser
      adminPassword: AdminPW
    }
    storageProfile: {
      imageReference: {
        publisher: 'MicrosoftWindowsServer'
        offer: 'WindowsServer'
        sku: OSVersion
        version: 'latest'
      }
      osDisk: {
        createOption: 'FromImage'
        managedDisk: {
          storageAccountType: 'Premium_LRS'
        }
      }
      dataDisks:[
        {
          name:'${VMName}_Data_01'
          diskSizeGB: int(DataDiskSize)
          lun: 0
          createOption: 'Empty'
        }
      ]    
  }
  networkProfile:{
    networkInterfaces:[
      {
        id:NewNic.id
      }
    ]
  }
}
}
